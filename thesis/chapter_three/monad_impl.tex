\subsection{Реализация монадического представления термов}

Термы в монадическом представлении кодируются следующим типом данных:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    -- F is for functor.
    \data FTerm (V : \Set)
        | FVar V
        | FApp (FTerm V) (FTerm V)
        | FLam (FTerm (Either V Unit))
  \end{minted}
  \caption{Тип данных, кодирующий термы в монадическом представлении.}
\end{listing}

Для абстракции можно воспользоваться типом \mintinline{text}{Maybe V}, нетрудно убедиться, что эти типы изоморфны между собой.

Для реализации монадического связывания можно заметить, что сигнатуру \texttt{bind} можно обобщить. Для краткости будем обозначать тип \mintinline{text}{Either V Unit} как $V + 1$. Если мы определим вспомогательную фукнцию $\texttt{bind'} : FTerm (V + n) \to (V \to FTerm\ W) \to FTerm (W + n) $, то \texttt{bind}, очевидно, будет определен, как её результат при $n = 0$. Её можно было бы определить примерно следующим образом:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    bind'
        {V W : \Type}
        (n : Nat)
        (t : FTerm (Telescope n V))
        (k : V -> FTerm W) : FTerm (Telescope n W) <= \elim t
            | FVar x        => twistT n (fmap-telescope n k x)
            | FApp t1 t2    => FApp (bind' n t1 k) (bind' n t2 k)
            | FLam t        => FLam (bind' (suc n) t k)


    \function
    bind
        {V W : \Type}
        (t : FTerm V)
        (k : V -> FTerm W) : FTerm W => bind' zero t k
  \end{minted}
  \caption{Один из вариантов определения \texttt{bind}.}
\end{listing}

Здесь \mintinline{text}{Telescope n V} -- это тип $V + n$, определяемый рекурсивно следующим образом:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    Telescope
        (n : Nat)
        (V : \Type) : \Type <= \elim n
            | zero  => V
            | suc n => Either (Telescope n V) Unit
  \end{minted}
  \caption{Тип $V + n$}
\end{listing}

Функции \texttt{fmap-telescope} и \texttt{twistT} являются в некотором роде аналогами \texttt{fmap} и \texttt{sequenceA} из класса типов Traversable в \textbf{Haskell}~\cite{traversable} для типа $V + n$. Проблема такого определения, что про него очень неудобно доказывать свойство ассоциативности \texttt{bind}. Поэтому от этого определения было решено отказаться в пользу определения, приведенного в разделе~\ref{sec:monad}.

Второй тонкий момент связан с реализацией преобразования из монадических термов в неименованные. Мы могли бы определить его следующим образом:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    delta
        {n : Nat}
        (t : FTerm (Fin n)) : ITerm n <= \elim t
            | FVar i        => IVar i
            | FApp t1 t2    => IApp (delta t1) (delta t2)
            | FLam t        => ILam (delta (fmap (\lam e => \case e | inl i => fsuc i | inr unit => fzero) t))
  \end{minted}
  \caption{Вариант определения преобразования монадического терма в неименованный.}
\end{listing}

Однако в случае абстракции становится неочевидно, что этот код завершается. Поэтому в качестве примера определим тип \mintinline{text}{Fun n} очень похожий на \mintinline{text}{Fin n}(на самом деле -- они изоморфны друг другу):

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    Fun
        (n : Nat) : \Type <= \elim n
            | zero  => Empty
            | suc n => Either (Fun n) Unit
  \end{minted}
  \caption{Тип \mintinline{text}{Fun n}.}
\end{listing}

Тогда преобразование в него определяется довольно просто:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    delta
        {n : Nat}
        (t : FTerm (Fun n)) : ITerm n <= \elim t
            | FVar f        => IVar (to-fin f)
            | FApp t1 t2    => IApp (delta t1) (delta t2)
            | FLam t        => ILam (delta {suc n} t)
  \end{minted}
  \caption{Работающий вариант определения преобразования монадического терма в неименованный.}
\end{listing}
