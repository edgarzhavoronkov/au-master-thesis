\subsection{Реализация именованного представления термов}

Термы в этом представлении кодируются следующим типом данных:
\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    -- N is for named
    \data NTerm
        | NVar Nat
        | NApp NTerm NTerm
        | NLam Nat NTerm
  \end{minted}
  \caption{Тип данных, кодирующий термы в именованном представлении.}
\end{listing}

Сразу же отметим, что в имена переменных мы берем из множества натуральных чисел. Мы делаем это по ряду причин. Во-первых, в языке бедная стандартная библиотека и символов в ней нет. Во-вторых, в описании именованного представления в разделе~\ref{sec:named} мы неявно предполагаем, что на множестве $\mathcal{V}$ равенство разрешимо, а для натуральных чисел в стандартной библиотеке языка есть доказательство этого факта. В дальнейшем, мы пользуемся им для определения, например, операции подстановки.

Альфа-эквивалентность определяется как следующий индуктивный предикат:
\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \data AlphaEq (t1 t2 : NTerm)
      | AlphaEq (NVar x) (NVar y)           => VarEq (x = y)
      | AlphaEq (NApp t1 s1) (NApp t2 s2)   => AppEq (AlphaEq t1 t2) (AlphaEq s1 s2)
      | AlphaEq (NLam x t1) (NLam y t2)     => LamEq (b : Nat) (b != x) (b != y) (b # t1) (b # t2) (AlphaEq (nsubst-var t1 x b) (nsubst-var t2 y b))
  \end{minted}
  \caption{Определение альфа-эквивалентности.}
\end{listing}

Обратим внимание на случай абстракции. Можно было бы определить его проще, как в разделе~\ref{sec:named}, но то определение имеет ряд недостатков. Предположим, что мы хотели бы доказать рефлексивность альфа-эквивалентности. В случае абстракции нам бы понадобилось свойство унитальности для подстановки. Но в доказательстве свойства унитальности так же используется рефлексивность. Получается своеобразная цепочка взаимно-рекурсивных определений, завершаемость которой крайне неочевидна для языка.

Здесь предикат \mintinline{text}{x # t} означает, что переменная \mintinline{text}{x} является <<свежей>> для терма \mintinline{text}{t}, то есть не встречается в списке переменных этого терма. Случай для абстракции означает примерно следующее -- две лямбды альфа-эквивалентны, если есть переменная \mintinline{text}{b}, отличная от \mintinline{text}{x} и \mintinline{text}{y}, свежая в термах \mintinline{text}{t1} и \mintinline{text}{t2}, такая что если подставить ее в тела в \mintinline{text}{t1} и \mintinline{text}{t2}, то они будут альфа-эквивалентны.

Для преобразования именованных термов в неименованные мы в явном виде определили предикат <<терм определен в контексте>>:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    VarInCtx {n : Nat} (env : Ctx n) (x : Nat) => \Sigma (i : Fin n) (p : env !! i = x)

    \data TermInCtx {n : Nat} (env : Ctx n) (t : NTerm)
      | TermInCtx {n} env (NVar x)     => Variable (p : VarInCtx env x)
      | TermInCtx {n} env (NApp t1 t2) => Application (p : TermInCtx env t1) (q : TermInCtx env t2)
      | TermInCtx {n} env (NLam x t)   => Abstraction (p : TermInCtx (extend env x) t)
  \end{minted}
  \caption{Предикат <<терм определен в контексте>>.}
\end{listing}

Имея его, можно определить отношение альфа-эквивалентности еще одним образом -- на тройках <<контекст, терм и доказательство того, что терм определен в контексте>>:

\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \data AlphaEq
        {n : Nat}
        (env1 : Ctx n)
        (t1 : NTerm)
        (p1 : TermInCtx env1 t1)
        (env2 : Ctx n)
        (t2 : NTerm)
        (p2 : TermInCtx env2 t2)
            | AlphaEq env1 (NVar x) (Variable p) env2 (NVar y) (Variable q)                       => VarEq (p : p.1 = q.1)
            | AlphaEq env1 (NApp t1 s1) (Application p1 q1) env2 (NApp t2 s2) (Application p2 q2) => AppEq (p : AlphaEq env1 t1 p1 env2 t2 p2) (q : AlphaEq env1 s1 q1 env2 s2 q2)
            | AlphaEq env1 (NLam x t1) (Abstraction p1) env2 (NLam y t2) (Abstraction p2)         => LamEq (p : AlphaEq (extend env1 x) t1 p1 (extend env2 y) t2 p2)
  \end{minted}
  \caption{Еще один вариант определения альфа-эквивалентности.}
\end{listing}

О контексте здесь можно думать, как о связывании переменных, тогда очень легко показать, что такое отношение сохраняется при преобразовании именованного терма в неименованный.

Операция подстановки могла бы быть определена следующим образом:
\begin{listing}[H]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    nsubst
        (t : NTerm)
        (y : Nat)
        (s : NTerm) : NTerm <= \elim t
            | NVar x     => \case x =? y | inr _ => s | inl _ => (NVar x)
            | NApp t1 t2 => NApp (nsubst t1 y s) (nsubst t2 y s)
            | NLam x t   => \let
                              | frv => gen-fresh-var t
                            \in \case isFreeDec x s | inr _ => (NLam frv (nsubst (nsubst t x (NVar frv)) y s)) | inl _ => (NLam x (nsubst t y s))
  \end{minted}
  \caption{Один из вариантов определения операции подстановки.}
\end{listing}

К сожалению этот вариант не проходит проверку на завершаемость в случае лямбда-абстракции. Отметим, что и в разделе~\ref{sec:named} в доказательстве леммы о подстановке мы весьма небрежно использовали индукционную гипотезу. Для неформальных рассуждений это еще допустимо, но для строго формальных -- уже нет. Чтобы решить эту проблему, мы определили более общий случай подстановки -- параллельную подстановку:

\begin{listing}[h]
  \begin{minted}[breaklines=true, frame=lines, linenos]{text}
    \function
    nsubst'
        (t : NTerm)
        (ps : List (\Sigma Nat NTerm)) : NTerm <= \elim t
            | NVar x        => lookup ps x
            | NApp t1 t2    => NApp (nsubst' t1 ps) (nsubst' t2 ps)
            | NLam x t      =>  \let
                                    | ts => (NLam x t) :-: (map (\lam (p : \Sigma Nat NTerm) => p.2) ps)
                                    | x' => gen-fresh-var ts
                                \in NLam x' (nsubst' t ((x, NVar x') :-: ps))

    \function
    nsubst
        (t : NTerm)
        (y : Nat)
        (s : NTerm) : NTerm => nsubst' t (singleton (y, s))
  \end{minted}
  \label{listing:parallel-subst}
  \caption{Вариант определения операции подстановки с помощью параллельной подстановки.}
\end{listing}

Вспомогательная функция \mintinline{text}{nsubst'} принимает на вход помимо терма уже не просто переменную и подставляемый терм, а список пар, из переменных и термов, которые необходимо подставить на их место. Этот вариант уже, очевидно, проходит проверку на завершаемость. К сожалению, из-за технических проблем в самом языке этот вариант определения подстановки появился ближе к концу процесса выполнения работы, поэтому мы не успели доказать про него лемму о подстановке в случае абстракции. Один из способов решить эту и ряд вышеперечисленных проблем -- использовать идеи, аналогичные тем, что применяются в номинальных множествах -- \cite{pitts2013nominal}.
